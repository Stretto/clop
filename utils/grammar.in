#  The MIT License (MIT)
#  =====================
#
#  Copyright (c) 2015 Dmitri Makarov <dmakarov@alumni.stanford.edu>
#
#  Permission is hereby granted, free of charge, to any person obtaining a copy
#  of this software and associated documentation files (the "Software"), to deal
#  in the Software without restriction, including without limitation the rights
#  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
#  copies of the Software, and to permit persons to whom the Software is
#  furnished to do so, subject to the following conditions:
#
#  The above copyright notice and this permission notice shall be included in all
#  copies or substantial portions of the Software.
#
#  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
#  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
#  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
#  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
#  SOFTWARE.

CLOP:

 TranslationUnit        <- ExternalDeclarations? KernelBlock
 KernelBlock            <- (SyncPattern :Spacing)? RangeDecl :Spacing CompoundStatement (:Spacing Transformations)?
 ExternalDeclarations   <- ExternalDeclaration (:Spacing ExternalDeclaration)*
 ExternalDeclaration    <  FunctionDefinition / Declaration
 FunctionDefinition     <  TypeSpecifier Declarator CompoundStatement
 SyncPattern            <  Identifier
 RangeDecl              <  "NDRange" '(' RangeList ')'
 RangeList              <  RangeSpec (',' RangeSpec)*
 RangeSpec              <  Identifier ':' ConditionalExpr ".." ConditionalExpr (local_work_size_specifier)?
 local_work_size_specifier < '$' ConditionalExpr
 Transformations        <  "apply" '(' TransList ')'
 TransList              <  TransSpec (',' TransSpec)*
 TransSpec              <  Identifier ('(' ')' / '(' ArgumentExprList ')')

# CLOP_Decl:
 Declaration            <- (DeclarationSpecifiers :Spacing) InitDeclaratorList? ';'
 Declarator             <  (Identifier / '(' Declarator ')')
                           ('[' ']' / '(' ')' / '[' ConditionalExpr ']' / '(' ParameterList ')' / '(' IdentifierList ')')*
 StorageClassSpecifier  <- "local"
 TypeSpecifier          <- "void" / "bool" / "char" / "uchar" /
                           "short" / "ushort" / "int" / "uint" / "long" / "ulong" /
                           "float" / "double" / "half" / "size_t" / StructSpecifier
 DeclarationSpecifiers  <- (StorageClassSpecifier :Spacing)? TypeSpecifier
 StructSpecifier        <  "struct" (Identifier ('{' StructDeclarationList '}')? / '{' StructDeclarationList '}')
 StructDeclarationList  <  StructDeclaration (:Spacing StructDeclaration)*
 StructDeclaration      <  TypeSpecifier StructDeclaratorList ';'
 StructDeclaratorList   <  StructDeclarator (',' StructDeclarator)*
 StructDeclarator       <  (Declarator ConditionalExpr? / ConditionalExpr)
 InitDeclaratorList     <  InitDeclarator (',' InitDeclarator)*
 InitDeclarator         <  Declarator ('=' Initializer)?
 InitializerList        <  Initializer (',' Initializer)*
 Initializer            <  AssignmentExpr / '{' InitializerList ','? '}'
 ParameterList          <  ParameterDeclaration (',' ParameterDeclaration)*
 ParameterDeclaration   <  TypeSpecifier Declarator
 TypeName               <  TypeSpecifier ('[' ']' / '[' ConditionalExpr ']') ('[' ']' / '[' ConditionalExpr ']')*

# CLOP_Stmt:
 StatementList          <- Statement (:Spacing Statement)*
 Statement              <  CompoundStatement / ExpressionStatement / IfStatement / IterationStatement / ReturnStatement / Declaration
 CompoundStatement      <  '{' StatementList? '}'
 ExpressionStatement    <  Expression ';'
 IfStatement            <  "if" '(' Expression ')' Statement ("else" Statement)?
 IterationStatement     <  WhileStatement / ForStatement
 WhileStatement         <  "while" '(' Expression ')' Statement
 ForStatement           <  "for" '(' BuiltinType? ExpressionStatement ExpressionStatement Expression? ')' Statement
 ReturnStatement        <  "return" Expression? ';'

# CLOP_Expr:
 PrimaryExpr            <  Identifier ('!' StringLiteral)? / FloatLiteral / IntegerLiteral / '(' Expression ')'
 PostfixExpr            <  PrimaryExpr ( '[' Expression ']'
                                       / '(' ')'
                                       / '(' ArgumentExprList ')'
                                       / '.' Identifier
                                       / "++"
                                       / "--" )*
 ArgumentExprList       <  AssignmentExpr (',' AssignmentExpr)*
 UnaryExpr              <  PostfixExpr
                         / IncrementExpr
                         / DecrementExpr
                         / UnaryOperator CastExpr
 UnaryOperator          <- [-+~!]
 IncrementExpr          < "++" UnaryExpr
 DecrementExpr          < "--" UnaryExpr
 CastExpr               <  UnaryExpr / '(' TypeName ')' CastExpr
 MultiplicativeExpr     <  CastExpr (MultiplicativeOperator MultiplicativeExpr)*
 MultiplicativeOperator <- [*%/]
 AdditiveExpr           <  MultiplicativeExpr (AdditiveOperator AdditiveExpr)*
 AdditiveOperator       <- [-+]
 ShiftExpr              <  AdditiveExpr (ShiftOperator ShiftExpr)*
 ShiftOperator          <- "<<" / ">>"
 RelationalExpr         <  ShiftExpr (RelationalOperator RelationalExpr)*
 RelationalOperator     <- "<=" / ">=" / "<" / ">"
 EqualityExpr           <  RelationalExpr (EqualityOperator EqualityExpr)*
 EqualityOperator       <- "==" / "!="
 ANDExpr                <  EqualityExpr ('&' ANDExpr)*
 ExclusiveORExpr        <  ANDExpr ('^' ExclusiveORExpr)*
 InclusiveORExpr        <  ExclusiveORExpr ('|' InclusiveORExpr)*
 LogicalANDExpr         <  InclusiveORExpr ("&&" LogicalANDExpr)*
 LogicalORExpr          <  LogicalANDExpr ("||" LogicalORExpr)*
 ConditionalExpr        <  LogicalORExpr ('?' Expression ':' ConditionalExpr)?
 AssignmentExpr         <  UnaryExpr AssignmentOperator AssignmentExpr / ConditionalExpr
 AssignmentOperator     <- "=" / "*=" / "/=" / "%=" / "+=" / "-=" / "<<=" / ">>=" / "&=" / "^=" / "|="
 Expression             <  AssignmentExpr (',' AssignmentExpr)*

# CLOP_Term:
 IdentifierList         <  Identifier (',' Identifier)*
 Identifier             <~ !Keyword [a-zA-Z_] [a-zA-Z0-9_]*
 Keyword                <- "NDRange" / "apply" / "popen" / "pclose" / "pipe"
                         / "auto" / "break" / "case" / "char" / "const" / "continue"
                         / "default" / "double" / "do" / "else" / "enum" / "extern"
                         / "float" / "for" / "goto" / "if" / "inline" / "int" / "long"
                         / "register" / "restrict" / "return" / "short" / "signed"
                         / "sizeof" / "static" / "struct" / "switch" / "typedef"
                         / "union" / "unsigned" / "void" / "volatile" / "while"
 BuiltinType            <~ ("u"? ("bool" / "char" / "short" / "int" / "long" / "half" / "float" / "double" / "quad") ("2" / "3" / "4" / "8" / "16")?)
                         / "image3d_t" / "image2d_t" / "sampler_t" / "event_t" / "void" / "ptrdiff_t" / "intptr_t" / "size_t"
 Spacing                <~ (space / endOfLine / Comment)*
 Comment                <~ "//" (!endOfLine .)* endOfLine
 IntegerLiteral         <~ Sign? Integer IntegerSuffix?
 Integer                <~ digit+
 IntegerSuffix          <- "Lu" / "LU" / "uL" / "UL" / "L" / "u" / "U"
 FloatLiteral           <~ Sign? Integer "." Integer? (("e" / "E") Sign? Integer)?
 StringLiteral          <~ doublequote (DQChar)* doublequote
 DQChar                 <- EscapeSequence
                         / !doublequote .
 EscapeSequence         <~ backslash ( quote
                                     / doublequote
                                     / backslash
                                     / [abfnrtv]
                                     )
 Sign                   <- "-" / "+"

# Local Variables:
# mode: Fundamental
# End:
